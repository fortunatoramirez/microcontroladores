## Práctica 9. Juego de reacción

### Objetivo general

Implementar un juego de reacción en un microcontrolador utilizando técnicas avanzadas de programación estructurada, manejo de estados, temporización sin bloqueos, lógica robusta para lectura de botones y control eficiente de múltiples salidas mediante Charlieplexing. El sistema debe operar de forma autónoma y reiniciarse solo mediante una pulsación larga del botón, sin requerir reinicio del microcontrolador.

---

### Fundamentos teóricos

Esta práctica integra los principales conceptos revisados durante la Unidad 2 del curso:

* **Temporización con `millis()`:** Se evita el uso de `delay()` para permitir una lógica no bloqueante. La variable `_millis` es incrementada artificialmente de forma manual para un control más fino.

* **Manejo de estados con `enum` y `switch-case`:** Se implementa una máquina de estados finita (FSM) que permite estructurar el juego en fases claramente definidas: inicio, espera, secuencia aleatoria, verificación de reacción y resultado.

* **Lectura robusta del botón:** Se aplica una lógica de antirrebote mediante el seguimiento de pulsaciones y un temporizador interno. Se distingue entre:

  * Pulsación corta (para interactuar con el juego).
  * Pulsación larga (para reiniciar el juego desde el estado de victoria o derrota).

* **Charlieplexing:** Técnica que permite controlar múltiples LEDs con un número reducido de pines. En esta práctica, 6 LEDs son controlados utilizando únicamente 3 pines, configurando dinámicamente los pines como entrada, salida o salida con nivel lógico alto/bajo.

* **Números aleatorios:** Se emplea `esp_random()` (para ESP32) con el fin de generar secuencias impredecibles en los LEDs, lo cual aumenta la dificultad del juego.

* **Prevención de desbordamiento:** La lógica está diseñada para seguir funcionando correctamente incluso si la variable `_millis` sobrepasa su valor máximo, evitando errores comunes asociados al desbordamiento de temporizadores.

---

### Material necesario

| Cantidad | Componente                            |
| -------- | ------------------------------------- |
| 1        | Microcontrolador (ESP32 o compatible) |
| 1        | Push button (botón de presión)        |
| 1        | Resistencia Pull-Down (\~10 kΩ)       |
| 6        | LEDs                                  |
| 3        | Resistencias para LEDs (\~150 Ω)      |
| 1        | Protoboard                            |
| Varios   | Jumpers                               |

---

### Conexiones

* **Charlieplexing (3 salidas):** Pines GPIO 12, 14 y 27 (o los que se desee) se conectan a los LEDs mediante resistencias limitadoras, permitiendo encender los 6 LEDs con combinaciones específicas.
* **Push button:** Conectado a GPIO 34 (o el que se desee), con resistencia Pull-Down de 10 kΩ a tierra.

---

### Desarrollo por bloques

#### Bloque 1: Definición de pines, variables y estructuras base

```cpp
byte salidas[] = {12,14,27};
byte estadoActual=0;
byte btn=3;
int ctr=0;
int r;
unsigned long _millis = 0;
unsigned long ts = 0;
unsigned int cuentaRegresiva = 0;
unsigned int cuentaProgresiva = 0;
unsigned int ctrLeds = 0;
unsigned int ctrSecGana = 0; 
unsigned int ctrSecPierde = 0;
bool banderaWin = HIGH;
bool prendidos = HIGH;
bool mitad = HIGH;
bool pressed = LOW;

enum { SAL1, SAL2, SAL3, TOTAL_SALIDAS };
enum { LED1, LED2, LED3, LED4, LED5, LED6, TOTAL_LEDS };
enum {
  INICIO,
  ESPERANDO,
  INICIA_CUENTA,
  TERMINA_CUENTA,
  TERMINA_JUEGO,
  GANASTE,
  PERDISTE,
  TOTAL_ESTADOS
};
```

#### Bloque 2: Configuración inicial

```cpp
void setup() {
  pinMode(34,INPUT);
}
```

#### Bloque 3: Ciclo principal (loop)

```cpp
void loop() {
  btn = revisarBoton();
  if(btn==1)
    estadoActual=INICIO;

  switch(estadoActual) {
    case INICIO:
      cuentaRegresiva = esp_random()%5000+2000;
      estadoActual++;
      break;

    case ESPERANDO:
      secuenciaContinua();
      if(btn == 0)
        estadoActual++;
      break;

    case INICIA_CUENTA:
      secuenciaAleatoria();
      cuentaRegresiva--;
      if(cuentaRegresiva==0) {
        apagarLeds();
        estadoActual++;
      }
      break;

    case TERMINA_CUENTA:
      cuentaProgresiva++;
      if(btn == 0)
        estadoActual++;
      break;

    case TERMINA_JUEGO:
      if(cuentaProgresiva > 1000)
        estadoActual = PERDISTE;
      else
        estadoActual = GANASTE;
      break;

    case GANASTE:
      secuenciaGana();
      break;

    case PERDISTE:
      secuenciaPierde();
      break;
  }

  delay(1);
  _millis++;
}
```

#### Bloque 4: Lectura robusta del botón

```cpp
int revisarBoton() {
  if(digitalRead(34) && !pressed) {
    ts = _millis;
    pressed = HIGH;
  }
  else if(!digitalRead(34) && pressed) {
    if(_millis-ts>60 && _millis-ts<2000) {
      pressed = LOW;
      return 0;
    }
    else if(_millis-ts>1999) {
      pressed = LOW;
      return 1;
    }
    else {
      pressed = LOW;
      return 2;
    }
  }
}
```

#### Bloque 5: Secuencias de LEDs

```cpp
void secuenciaGana() {
  if(_millis%250 == 0)
    prendidos = !prendidos;

  if(prendidos)
    prenderLed(ctrSecGana % TOTAL_LEDS);
  else
    apagarLeds();

  ctrSecGana++;
}

void secuenciaPierde() {
  if(_millis%350 == 0)
    mitad = !mitad;

  if(mitad)
    prenderLed(ctrSecPierde % (TOTAL_LEDS / 2));
  else
    prenderLed((ctrSecPierde % (TOTAL_LEDS / 2)) + 3);

  ctrSecPierde++;
}

void secuenciaAleatoria() {
  if(_millis%100==0)
    prenderLed(esp_random()%TOTAL_LEDS);
}

void secuenciaContinua() {
  if(_millis%100==0)
    prenderLed(ctrLeds++ % TOTAL_LEDS);
}
```

#### Bloque 6: Control de LEDs (Charlieplexing)

```cpp
void apagarLeds() {
  for(byte i = SAL1; i < TOTAL_SALIDAS; i++)
    pinMode(salidas[i], INPUT);
}

void prenderLed(byte led) {
  switch(led) {
    case LED1:
      pinMode(salidas[SAL1], OUTPUT); digitalWrite(salidas[SAL1], LOW);
      pinMode(salidas[SAL2], OUTPUT); digitalWrite(salidas[SAL2], HIGH);
      pinMode(salidas[SAL3], INPUT);
      break;
    case LED2:
      pinMode(salidas[SAL1], OUTPUT); digitalWrite(salidas[SAL1], HIGH);
      pinMode(salidas[SAL2], OUTPUT); digitalWrite(salidas[SAL2], LOW);
      pinMode(salidas[SAL3], INPUT);
      break;
    case LED3:
      pinMode(salidas[SAL1], INPUT);
      pinMode(salidas[SAL2], OUTPUT); digitalWrite(salidas[SAL2], LOW);
      pinMode(salidas[SAL3], OUTPUT); digitalWrite(salidas[SAL3], HIGH);
      break;
    case LED4:
      pinMode(salidas[SAL1], INPUT);
      pinMode(salidas[SAL2], OUTPUT); digitalWrite(salidas[SAL2], HIGH);
      pinMode(salidas[SAL3], OUTPUT); digitalWrite(salidas[SAL3], LOW);
      break;
    case LED5:
      pinMode(salidas[SAL1], OUTPUT); digitalWrite(salidas[SAL1], LOW);
      pinMode(salidas[SAL2], INPUT);
      pinMode(salidas[SAL3], OUTPUT); digitalWrite(salidas[SAL3], HIGH);
      break;
    case LED6:
      pinMode(salidas[SAL1], OUTPUT); digitalWrite(salidas[SAL1], HIGH);
      pinMode(salidas[SAL2], INPUT);
      pinMode(salidas[SAL3], OUTPUT); digitalWrite(salidas[SAL3], LOW);
      break;
  }
}
```

---

### Actividad de comprobación

**Indicaciones para el alumno:**

1. Completar el juego de reacción y verificar su funcionamiento conforme a la máquina de estados descrita.
2. Asegurarse de que:

   * El juego inicie con una secuencia continua.
   * Se reproduzca una secuencia aleatoria breve.
   * El usuario deba reaccionar justo después de que se apaguen los LEDs.
   * Una pulsación corta indique la reacción del usuario.
   * Una pulsación larga reinicie el juego (sin usar el botón de reset).
3. De ser necesario, realizar ajustes en la práctica. Posteriormente, modificar para no utilizar delay en el incremento de _millis y para evitar un error en el desbordamiento de la misma. Una vez que el juego haya quedado funcionando hasta este punto; Aplicar las técnicas y conocimientos aprendidos en las prácticas anteriores, para tener un juego más robusto, de buen diseño y con caractgerísticas útiles y agradables.

---

### Notas finales

* Esta práctica puede ser evaluada como examen práctico si se completan los tres puntos satisfactoriamente dentro del tiempo asignado.
* Si se logra el punto anterior; subir la documentación del proceso de desarrollo de la práctica, descricpión adecuada, códigos, capturas, fotografías, etc. Y Renombrándola a "Examen práctico: juego de reacción".


---

